# -*- python -*-
# Qserv qmeta module SConscript
import distutils.sysconfig
import pytarget
import cctarget

Import('env')
Import('extraTgts')
Import('extraSrcs')

# this assumes that we use the same Python for scons and final products
pythonInc = distutils.sysconfig.get_python_inc()

cppPath = cctarget.getList(env, 'CPPPATH') + [pythonInc]
swigged = env.SharedObject(source="qmetaLib.i",
                            SWIGFLAGS=['-python', '-c++', '-Iinclude'],
                            SWIGPATH=['..', pythonInc],
                            CPPPATH=cppPath,
                            LDMODULEPREFIX='_', LDMODULESUFFIX='.so'
                            )

extraSrcs["qmeta"] = swigged

# # Create entry for python file, which SCons SWIG builder ignores
pySwig = env.File('qmetaLib.py')
extraTgts["qmetaLib.py"] = pySwig

pyFiles = pytarget.getPyTargets(env, ["lsst", "qserv", "qmeta"], [pySwig])
extraTgts['dist'].extend(pyFiles)

defaultTargets = cctarget.getDefaultTargets(env, '.', lambda f: str(f) == "qmetaLib_wrap.cc")

Return('defaultTargets')

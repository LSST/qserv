# -*- python -*-
import distutils.sysconfig

Import('env')
Import('standardModule')

standardModule(env, unit_tests=[], exclude='./qmetaLib_wrap.cc')

build_data = DefaultEnvironment()['build_data']

# this assumes that we use the same Python for scons and final products
pythonInc = distutils.sysconfig.get_python_inc()

cppPath = env['CPPPATH'] + [pythonInc]
swigged = env.SharedObject(source="qmetaLib.i",
                            SWIGFLAGS=['-python', '-c++', '-Iinclude'],
                            SWIGPATH=['..', pythonInc],
                            CPPPATH=cppPath,
                            LDMODULEPREFIX='_', LDMODULESUFFIX='.so'
                            )

build_data['module_objects']['qmeta:python'] = swigged

# Create entry for python file, which SCons SWIG builder ignores
pySwig = env.File('qmetaLib.py')

# install python file
dst = "$prefix/$python_relative_prefix/lsst/qserv/qmeta"
py_installed = env.Install(dst, pySwig)
build_data['install'] += py_installed

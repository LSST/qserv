# -*- python -*-
import distutils.sysconfig

Import('env')
Import('standardModule')

standardModule(env, unit_tests="testKvInterfaceImpl testCssAccess testEmptyChunks",
               exclude='./cssLib_wrap.cc')

build_data = DefaultEnvironment()['build_data']

# this assumes that we use the same Python for scons and final products
pythonInc = distutils.sysconfig.get_python_inc()

cppPath = env['CPPPATH'] + [pythonInc]
swigged = env.SharedObject(source="cssLib.i",
                           SWIGFLAGS=['-python', '-c++', '-Iinclude'],
                           SWIGPATH=['..', pythonInc],
                           CPPPATH=cppPath,
                           LDMODULEPREFIX='_', LDMODULESUFFIX='.so'
                           )

build_data['module_objects']['css:python'] = swigged

# Create entry for python file, which SCons SWIG builder ignores
pySwig = env.File('cssLib.py')

# install python file
dst = "$prefix/$python_relative_prefix/lsst/qserv/css"
py_installed = env.Install(dst, pySwig)
build_data['install'] += py_installed

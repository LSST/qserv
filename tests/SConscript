# -*- python -*-
import subprocess, os, sys

Import("env")

env.Append(LIBPATH = ["../lib"])
env.Append(LIBS = ["qserv_worker"])

def run_test(target, source, env):
    log = file(str(target[0]) + ".failed", "w")
    ret = subprocess.call(str(source[0]),
            stdout=log, stderr=log, env=os.environ)
    log.close()
    if ret == 0:
        os.rename(str(target[0]) + ".failed", str(target[0]))
    else:
        print >> sys.stderr, "*** TEST FAILED ***", source[0]
    return ret

bld = Builder(action = run_test, prefix = ".tests/")
env.Append(BUILDERS = {'Test': bld})
for src in Glob("*.cc"):
    prog = env.Program(src)
    test = env.Test(prog)
    env.Depends(test, env['built_libs'])
